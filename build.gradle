import net.researchgate.release.GitAdapter

buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
    }
}

plugins {
    id 'com.github.hierynomus.license' version '0.13.1'
    id 'net.researchgate.release' version '2.6.0'
    id 'io.codearte.nexus-staging' version '0.9.0'
    id 'com.palantir.docker' version '0.13.0'
    id 'org.dm.bundle' version '0.10.0'
}

ext {
    name = 'apix broadcast and indexing'
    description = 'indexing services'
    url = 'https://github.com/pan-dora/apix-connector-triplestore/'
    inceptionYear = '2017'
    vendor = 'pandorasystems'
    homepage = 'https://pan-dora.github.io/'
    license = 'Apache 2'
}

apply plugin: 'com.github.hierynomus.license'
license {
    include "**/*.java"
    header rootProject.file('src/main/resources/LICENSE')
    strictCheck true
    mapping {
        java = 'SLASHSTAR_STYLE'
    }
}

configure(allprojects) { project ->
    apply plugin: 'maven'
    apply plugin: 'java'
    apply plugin: 'org.dm.bundle'

    group = 'cool.pandora.exts'

    repositories {
        jcenter()
        mavenCentral()
        mavenLocal()
    }

    ext.camelVersionRange = '[2.18,3)'
    ext.activemqVersionRange = '[5.14,6)'
    ext.fcrepoCamelVersionRange = '[4.5,5)'
    ext.activemqVersion = 'LATEST'
    ext.camelVersion = '2.19.2'
    ext.acrepoKarafVersion = '1.1.0'
    ext.fcrepotoolboxVersion = "4.7.2"
    ext.fcrepoCamelVersion = '4.5.0'
    ext.defaultOsgiImports = 'org.osgi.service.blueprint;version="[1,2)",*'
    ext.projectOsgiVersion = project.version.replaceAll("-SNAPSHOT", ".SNAPSHOT")

    compileJava {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
        options.encoding = 'UTF-8'
    }

    compileTestJava {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
        options.encoding = 'UTF-8'
        options.compilerArgs += "-parameters"
    }

    processResources {
        filesMatching(["**/Dockerfile", "**/*.cfg"]) {
            expand(project.properties)
        }
    }

    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs.add("-Xlint:unchecked")
            options.compilerArgs.add("-Xlint:deprecation")
//                options.compilerArgs.addAll(["--add-modules", "java.se.ee"])
        }
    }

    compileTestJava {
        doFirst {
//                options.compilerArgs.addAll(['--add-modules', 'java.se.ee'])
            options.compilerArgs.add("-Xlint:deprecation")
        }
    }

    release {
        tagTemplate = '$name-$version'
        scmAdapters = [
                GitAdapter
        ]

        git {
            requireBranch = 'master'
            pushToRemote = 'origin'
            pushToBranchPrefix = ''
            commitVersionFileOnly = false
        }
    }

    def dockerResourceDir = "${rootProject.buildDir}/resources/main/docker"
    def mavenInstallPath = "repository/cool/pandora/exts"

    docker {
        name 'pandorasystems/messaging'
        tags "${version}"
        dockerfile file("${dockerResourceDir}/Dockerfile")
        files "${dockerResourceDir}/cfg", "${dockerResourceDir}/entrypoint.sh"
        copySpec.from("${buildDir}/maven").into("${mavenInstallPath}")
        pull true
        noCache true
    }

    def homeDir = System.getProperty('user.home')
    def mavenLocal = new File(homeDir, ".m2/repository/cool/pandora/exts")

    task copyTask(type: Copy) {
        from mavenLocal
        into "${rootProject.buildDir}/maven"
    }

    copyTask.dependsOn install

}

configure(subprojects) { subproject ->
    apply plugin: 'com.bmuschko.nexus'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'
    apply plugin: 'checkstyle'
    apply plugin: 'jacoco'

    task processConfig(type: Copy) {
        from('src/main/cfg') {
            include '**/*.cfg'
        }
        into 'build/cfg/main'
    }

    task sourceJar(type: Jar) {
        classifier 'sources'
        from sourceSets.main.allSource
    }

    artifacts {
        archives sourceJar
    }

    classes {
        classes.dependsOn processConfig
    }

    checkstyle {
        configFile = rootProject.file('src/main/resources/checkstyle/checkstyle.xml')
        configProperties.checkstyleConfigDir = rootProject.file('src/main/resources/checkstyle/')
        toolVersion = "8.1"
    }

    jacoco {
        toolVersion = "0.7.9"
    }

    publishing {
        publications {
            maven(MavenPublication) {
                from components.java
            }
        }
        repositories {
            maven {
                url "${rootProject.buildDir}/maven"
            }
        }
    }

    bundle {
        includeTransitiveDependencies = true
        passProjectProperties = false
    }

    modifyPom {
        //noinspection GroovyAssignabilityCheck
        project {
            name name
            description description
            url url
            inceptionYear inceptionYear

            organization {
                name vendor
                url homepage
            }

            developers {
                developer {
                    id 'christopher-johnson'
                    name 'Christopher Johnson'
                    email 'chjohnson @ (domain of organization url)'
                    organization vendor
                    organizationUrl homepage
                    roles {
                        role 'developer'
                    }
                    timezone '+1'
                }
            }

            scm {
                connection 'scm:git:git://github.com/pan-dora/apix-connector-triplestore.git'
                developerConnection 'scm:git:git@github.com/pan-dora/apix-connector-triplestore.git'
                url 'https://github.com/pan-dora/apix-connector-triplestore/'
                tag 'HEAD'
            }

            licenses {
                license {
                    name 'Apache License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0'
                }
            }
        }
    }

    nexusStaging {
        packageGroup = "cool.pandora"
        stagingProfileId = "4d5f13c386f543"
    }

    nexus {
        sign = true
        repositoryUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
        snapshotRepositoryUrl = 'https://oss.sonatype.org/content/repositories/snapshots/'
    }
}

project("exts-connector-triplestore") {

    description = 'Triplestore Indexer'
    dependencies {
        compile group: 'org.apache.camel', name: 'camel-core', version: camelVersion
        compile group: 'org.apache.camel', name: 'camel-http4', version: camelVersion
        compile group: 'org.apache.camel', name: 'camel-blueprint', version: camelVersion
        compile(group: 'org.fcrepo.camel', name: 'fcrepo-camel', version: fcrepoCamelVersion) {
            exclude(module: 'slf4j-log4j12')
        }
    }

    jar {
        manifest {
            attributes 'Import-Package': "org.apache.camel,org.fcrepo.camel,org.apache.camel.component.http4,${defaultOsgiImports}",
                'Export-Package': "cool.pandora.exts.connector.triplestore;version=${projectOsgiVersion}"
        }
    }

    artifacts {
        archives(file('build/cfg/main/cool.pandora.exts.connector.triplestore.cfg')) {
            classifier 'configuration'
            type 'cfg'
        }
    }
}

project("messaging-karaf") {

    description = 'Karaf Provisioning Features for repository services'

    processResources {
        outputs.upToDateWhen { false }
        expand project.properties
    }

    artifacts {
        archives(file('build/resources/main/features.xml')) {
            classifier 'features'
            type 'xml'
        }
    }

    jar {
        manifest {
            attributes 'Import-Package': "${defaultOsgiImports}",
                    'Export-Package': "cool.pandora.exts.sparqler-karaf;version=${projectOsgiVersion}"
        }
    }
}


